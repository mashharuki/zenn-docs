---
title: "Aptos上にNFTマーケットプレイスを作ってみよう！！"
emoji: "📖"
type: "tech" 
topics: ["Move", "Aptos", "Blockchain", "Web3", "NFT"]
published: false
---

# はじめに

皆さん、こんにちは！

今回はパブリックブロックチェーンの一つであるAptosをテーマにした記事になります！

2024年現在 ハッカソンプラットフォームAkindoとAptosのチームがタッグ組んでWaveHackというグラントプログラムを実施中です！

グラントには2つの部門があるのですが、今回はそのうちの一つである

**Create Aptos Move Contents** 

部門への応募も兼ねた記事になっています！！

対象となっているAptosのドキュメントを翻訳するだけでグラントを取得できる可能性があり、非エンジニアでもグラントを獲得できる非常に貴重な機会となっていますので皆さんもぜひ挑戦してみてくださいね！！

詳細は下記サイトにて紹介されています！

https://app.akindo.io/wave-hacks/Nmqgo639ai9ZQMB1


https://app.akindo.io/wave-hacks/Z47L4rwQmI774vRpr


https://lu.ma/aptos-wavehack

# 今回翻訳に挑戦するドキュメント

Aptosチームが出しているラーニングサイトの以下の記事の翻訳に挑戦します！！

https://learn.aptoslabs.com/example/nft-marketplace

Aptos上でたまごっちライクなキャラクターを売買できるNFTマーケットプレイスが構築できるようです！！

https://marketplace-example-rho.vercel.app/

![](/images/a9c7f813c75e8b/1.png)

前回の記事が見てみたいという方はぜひ下記もチェックしてみてください！！

https://zenn.dev/mashharuki/articles/f6deb29bb67cd3

今回お手本となるGitHubのリポジトリは下記です！

https://github.com/aptos-labs/marketplace-example

私は上記のリポジトリをベースにコメントなどを追記しています！！

https://github.com/mashharuki/marketplace-example

この学習コンテンツでは次のことが学べます！！

:::message
1. Move言語による応用的なデータ構造の実装方法

    NFTマーケットプレイスの機能を実現させるためには複雑なデータ構造を管理する必要がありますが、 Move言語でそれを実現させる方法を学べます。

2. entryメソッドの実装方法

    NFTの購入やリスティングなど、マーケットプレイスに最低限必要な機能をMove言語で実装する方法を学べます。

3. Move言語のテストコードの実装方法

    Move言語で開発したスマートコントラクトのテストコードの実装方法を学べます。
:::


この学習コンテンツで開発するNFTマーケットプレイスの概要

:::message
前回の記事までで開発していたAptogochiNFT用のNFTマーケットプレイスを開発します。

より使いやすいマーケットプレイスを開発するとなるとオークション機能があったりした方が良いですが、今回は基本的な機能の実装がメインとなります。

- NFTをリスティングさせる機能  
  自分が保有するNFTを売りに出す機能
- NFTを購入する機能  
  売りに出したユーザーが設定した金額でNFTを購入できるようにする機能
- NFTをミントする機能  
  デモ用にNFTミントする機能
:::

# Move言語によるデータストレージの考え方

データを永続的に保管する方法として以下の3つの方法があります。

- アカウント内のリソース  

  アカウント内のリソースでは、アカウントやアドレスに直接結びついたMoveのストレージ機能を利用できます。アカウントタイプごとに一意に保存され、保存にはmove_to、取得にはborrow_globalといった特定の関数でアクセスできます。  
  
  構造体は、アカウントにステートをアタッチする簡単な方法を提供しますが、ガスコストと単一リソースのサイズ制約により、ストレージに制限があります。これらの制限を回避するために、構造体には他の構造体やベクトルを含めることができますが、その代わり複雑になります。

- テーブル  

  Moveのテーブルでは、柔軟なキーとバリューを管理できます。同じ構造体タイプの複数のインスタンスを衝突リスクなしに使用できるため、リソースとは異なります。テーブルは、大規模なデータセットへの効率的で決定論的なアクセスを必要とするアプリケーションで特に役立ちます。  
  
  Aptosでは、スケーラブルなデータ処理を実現するBigVectorやSmartVector、凝縮されたストレージを実現するSmartTableなどの構造体でテーブルを強化し、グローバル・ストレージのスペースとアクセス効率を最適化しています。

- オブジェクト  

  オブジェクトは、データ・ストレージに対する微妙なアプローチを提供し、リソースをアカウントや他のオブジェクトによって所有・管理できるようにすることで、オンチェーン・データの構成能力を高めます。
  
  このモデルは、1つのアカウントや契約の下で複数のリソースインスタンスを管理することを容易にし、オブジェクトの所有権と転送性を活用することで、さまざまなドメインにわたる複雑なアプリケーションの開発を合理化します。

# 今回実装するスマートコントラクトのデータ構造のポイント

NFTマーケットプレイスのデータ構造を設計する際には、出品と取引を自律的に管理する方法が必要です。

このプロセスを促進するためには、価格、売り手の詳細、トークンのアドレスといった重要な情報をオンチェーンに保存することが必須です。

例えば下記のような点ですね。

1. 売り手ごとに複数のリスティングを保存する  

   売り手は異なるトークンについて複数のリスティングを作成できるようにする。

   例えば、Alice は複数の Aptogotchi トークンをリストし、それぞれに固有のリスト情報を持つかもしれません。

   そのためには、一つの売り手に関連する、同じ型の複数のインスタンスをサポートするデータ構造が必要です。

2. リスティングのライフサイクルの管理  

   リスティングにはライフサイクルがあり、トークンの販売で終了します。

   購入後、マーケットプレイスの整合性を維持するために、リスティングデータを効率的にクリーンアップして削除することが重要になってきます。


これらの要件を実現させるために、アカウントごとにリソースのインスタンスを1つだけ保存するという制限を克服するために、リストデータをオブジェクトとして保存します。

この方法は、売り手ごとに複数の出品を可能にするだけでなく、デジタル資産標準を実装するためにオブジェクトを使用する時と同じです。

Aptosのオブジェクトモデルについてさらに知りたいという方は下記リンクを参考にしてみてください。

https://aptos.dev/standards/aptos-object/

# オブジェクトリソースの定義

Moveでは、構造体にキーの能力をマークし、`resource_group_member`というアノテーションを付けてオブジェクト・リソースを定義します。これにより、リストごとに個別のオブジェクト アカウントを作成し、関連するすべてのリスト情報をその中にカプセル化することができます。

ここでは、2つの主要な構造体を中心にマーケットプレイスを設計します

- `Listing`
- `FixedPriceListing`

の2つです。

このように分離することで、オークションのような動的な価格モデルをサポートするなど、将来の拡張性を確保できます。
各リスティングは独自のオブジェクトアカウントを作成し、Listingはトークン オブジェクトと売り手情報を保持し、FixedPriceListingは選択したCoinTypeで価格を指定します（CoinTypeは一般的な型パラメータとして指定されます）。

## Listing リソース

今回のマーケットプレイスアプリでは下記の様に実装します。

```rs
#[resource_group_member(group = aptos_framework::object::ObjectGroup)]
struct Listing has key {
  /// The item owned by this listing, transferred to the new owner at the end.
	object: Object<ObjectCore>,
  /// The seller of the object.
	seller: address,
  /// Used to clean-up at the end.
	delete_ref: DeleteRef,
  /// Used to restore the object signer for transferring the escrowed object to the buyer.
	extend_ref: ExtendRef,
}

```

- object：   
  リストされるトークンオブジェクト。ここでは、token v2をサポートしています。これは、トークン・オブジェクトへのオブジェクト参照（またはポインタ）です。
- seller：   
  売り手アカウントのアドレス。
- delete_ref:   
  購入後にオブジェクトをクリーンアップするために必要です。
- extend_ref：  
  リスティング後、トークンをエスクローします（リスティングオブジェクトの署名者がトークンを保持します）ので、買い手が購入したいときにオブジェクトの署名者を復元し、object::transferを呼び出すためにExtendRefが必要です。

## FixedPriceListing リソース

今回のマーケットプレイスアプリでは下記の様に実装します。

```rs
#[resource_group_member(group = aptos_framework::object::ObjectGroup)]
struct FixedPriceListing<phantom CoinType> has key {
  /// The price to purchase the item up for listing.
	price: u64,
}
```

- price:   
 　リスティングしたNFTの価格です。

# エントリーメソッドの実装

必要なデータを保存する部分の実装が完了したので次に必要な機能を実装していきたいと思います。

ここでは2つのメソッドを実装していきます。


- `list_with_fixed_price`  
  売り手がトークンを売りに出す関数
- `purchase`  
  買い手がリストされたトークンを購入する関数

## list_with_fixed_price

このメソッドでNFTをリスティングさせることができます。

この関数では、売り手が所有する新しいオブジェクトを作成し、ListingリソースとFixedPriceListingリソースをオブジェクトに移動します。また、トークンをオブジェクトの署名者に転送し、エスクロー口座として機能させます。

objectモジュールから`create_object`関数を呼び出して、売り手のアカウントから新しいオブジェクトを作成します。このオブジェクトにListingとFixedPriceListingのリソースを格納します。

具体的には下記の様な実装となります。

```rs
/// List an time for sale at a fixed price.
public entry fun list_with_fixed_price<CoinType>(
    seller: &signer,
    object: Object<ObjectCore>,
    price: u64,
) {
    let constructor_ref = object::create_object(signer::address_of(seller));
    let transfer_ref = object::generate_transfer_ref(&constructor_ref);
    object::disable_ungated_transfer(&transfer_ref);

    let listing_signer = object::generate_signer(&constructor_ref);
    let listing = Listing {
        object,
        seller: signer::address_of(seller),
        delete_ref: object::generate_delete_ref(&constructor_ref),
        extend_ref: object::generate_extend_ref(&constructor_ref),
    };
    let fixed_price_listing = FixedPriceListing<CoinType> {
        price,
    };
    move_to(&listing_signer, listing);
    move_to(&listing_signer, fixed_price_listing);

    object::transfer(seller, object, signer::address_of(&listing_signer));
}
```

重要なポイントを解説していきます。

```rs
object::disable_ungated_transfer(&transfer_ref);
```

ここでは、オブジェクトのモジュールである`disable_ungated_transfer`関数を呼び出すことで、オブジェクトのゲートなし転送を不可にします。

```rs
let listing_signer = object::generate_signer(&constructor_ref);
let listing = Listing {
    object,
    seller: signer::address_of(seller),
    delete_ref: object::generate_delete_ref(&constructor_ref),
    extend_ref: object::generate_extend_ref(&constructor_ref),
};
let fixed_price_listing = FixedPriceListing<CoinType> {
    price,
};
move_to(&listing_signer, listing);
move_to(&listing_signer, fixed_price_listing);
```

新しいListingとFixedPriceListingリソースを作成し、オブジェクト署名者(ここではこのスマートコントラクト)に移動します。

```rs
object::transfer(seller, object, signer::address_of(&listing_signer));
```

最後にオブジェクトをオブジェクト署名者に転送してリスティングが完了します。

## purchase

購入者は、固定価格リストから NFT を購入する際にこの関数を呼び出します。

この関数は、NFT を買い手に譲渡し、代金を売り手に譲渡します。また、Listing および FixedPriceListing リソースを含むオブジェクトを削除します。

具体的には下記の様に実装します。

```rs
/// Purchase outright an item from a fixed price listing.
public entry fun purchase<CoinType>(
  purchaser: &signer,
  object: Object<ObjectCore>,
) acquires FixedPriceListing, Listing {
  let listing_addr = object::object_address(&object);
  assert!(exists<Listing>(listing_addr), error::not_found(ENO_LISTING));
  assert!(exists<FixedPriceListing<CoinType>>(listing_addr), error::not_found(ENO_LISTING));

  let FixedPriceListing {
      price,
  } = move_from<FixedPriceListing<CoinType>>(listing_addr);

  // The listing has concluded, transfer the asset and delete the listing. Returns the seller
  // for depositing any profit.
  let coins = coin::withdraw<CoinType>(purchaser, price);

  let Listing {
      object,
      seller, // get seller from Listing object
      delete_ref,
      extend_ref,
  } = move_from<Listing>(listing_addr);

  let obj_signer = object::generate_signer_for_extending(&extend_ref);
  object::transfer(&obj_signer, object, signer::address_of(purchaser));
  object::delete(delete_ref); // Clean-up the listing object.

  aptos_account::deposit_coins(seller, coins);
}
```

重要なポイントを解説していきます。

```rs
assert!(exists<Listing>(listing_addr), error::not_found(ENO_LISTING));
assert!(exists<FixedPriceListing<CoinType>>(listing_addr), error::not_found(ENO_LISTING));
```

ここでは、指定されたオブジェクト・アドレスに Listing および FixedPriceListing リソースが存在するかどうかを、存在するネイティブ関数を使ってチェックします。

```rs
let FixedPriceListing {
    price,
} = move_from<FixedPriceListing<CoinType>>(listing_addr);

// The listing has concluded, transfer the asset and delete the listing. Returns the seller
// for depositing any profit.
let coins = coin::withdraw<CoinType>(purchaser, price);
```

ここでは、FixedPriceListingリソースをグローバルストレージ（オブジェクトアドレスの下）から移動し、NFTの価格を抽出します。次に、coin::withdraw 関数を使って購入者のアドレスから代金分のトークンを引き出します。

```rs
let Listing {
    object,
    seller, // get seller from Listing object
    delete_ref,
    extend_ref,
} = move_from<Listing>(listing_addr);
```

ここでは、リスティング・リソースをグローバル・ストレージ（オブジェクト・アドレスの下）から移動し、オブジェクト、セラー、delete_ref、extend_refフィールドを抽出しています。次の処理に備えるためですね。

```rs
let obj_signer = object::generate_signer_for_extending(&extend_ref);
object::transfer(&obj_signer, object, signer::address_of(purchaser));
object::delete(delete_ref); // Clean-up the listing object.
```

ここでは、NFTを買い手に譲渡し、ListingとFixedPriceListingリソースを含むオブジェクトを削除しています。また、extend_refを使用してオブジェクトの署名者を生成し、エスクローされたNFTを買い手に譲渡しています。

```rs
aptos_account::deposit_coins(seller, coins);
```

最後にaptos_account::deposit_coins関数を使用してNFTの販売者に支払いを送金します。

Move言語によるメソッドの実装方法についてさらに知りたい人は下記リンクを参照してください！！

https://aptos.dev/move/book/functions/



